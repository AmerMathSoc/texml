#!/usr/bin/perl -w

use v5.26.0;

our $VERSION = v0.0.0;

use warnings;

use utf8;

my %keyword = map { $_ => 1 } qw(alfa and array begin boolean byte case char const div
                 do downto else end false file for function get goto
                 if in integer label maxint mod new not of or pack
                 packed page program put procedure read readln real
                 record repeat reset rewrite set text then to true
                 type unpack until var while with write writeln);

my $identifier = qr{[a-z][a-z0-9_]*}i;
my $comment    = qr{ (?: \{.*?\} | \(\* .*? \*\) )}smx;

my $b_string = qr{" (?: \\" | [^"] )* "}smx;
my $d_string = qr{' (?: '' | [^'] )* '}smx;

my $string   = qr{(?: $b_string | $d_string )}smx; # ???

my $token_r = qr{(?: $identifier | $string | $comment )}smx;

my $file = shift;

sub id {
    my $type = shift;
    my $token = shift;

    print STDERR qq{[$type, "$token"]\n};
}

sub output_filler {
    my $filler = shift;

    return unless length $filler;

    $filler =~ s{ }{\\ }g;

    $filler =~ s{\{}{\\\{}g;

    print $filler;
}

print << 'EOF';
\documentclass{amsart}

\begin{document}

\begingroup
\obeyspaces
\obeylines
EOF

open(my $fh, "<", $file) or die "Can't open $file: $!\n";

while (<$fh>) {
    while(length($_)) {
        s{^(.*?)($token_r)}{} and do {
            my $pre   = $1;
            my $token = $2;

            if (length $pre) {
                output_filler($pre);

                id(interstitial => $pre);
            }

            if ($token =~ m{\A$identifier\z}) {
                if ($keyword{lc $token}) {
                    id(keyword => $token);

                    print qq{\\textbf{$token}};
                } else {
                    id(identifier => $token);

                    print $token;
                }
            } elsif ($token =~ m{\A$string\z}) {
                id(string => $token);

                print qq{\\texttt{$token}};
            } elsif ($token =~ qr{\A$comment\z}) {
                id(comment => $token);

                $token =~ s{([{}])}{\\$1}g;

                print qq{\\textit{$token}};
            } else {
                id(wtf => $token);
            }

            next;
        };

        if (length) {
            output_filler($_);

            id(trailing => $_);
        }

        last;
    }
}

close($fh);

print << 'EOF';
\endgroup

\end{document}
EOF

__END__
